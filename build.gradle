/*
 * Copyright (c) xgraza 2025
 */

plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'com.github.gmazzo.buildconfig' version "${build_config_version}"
}

version = project.version
group = project.group

buildConfig {
    buildConfigField(String, "HASH", "${execute("git rev-parse HEAD")}")
    buildConfigField(String, "BRANCH", "${execute("git branch --show-current")}")
    buildConfigField(String, "GROUP", project.group.toString())
    buildConfigField(String, "VERSION", project.version.toString())
    buildConfigField(String, "BUILD", project.build_number.toString())
    buildConfigField(String, "BUILD_TIME", "${new Date(System.currentTimeMillis()).format("MM/dd/yyyy HH:mm")}")

    packageName project.group
    useJavaOutput()
    generateAtSync = true
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

base {
    archivesName = project.archives_base_name
}

repositories {

}

loom {
    mods {
        "modid" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(generateBuildConfig)
}

def execute(String cmd) {
    try {
        def stdout = new ByteArrayOutputStream()

        exec {
            commandLine cmd.split(" ")
            standardOutput = stdout
        }

        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}